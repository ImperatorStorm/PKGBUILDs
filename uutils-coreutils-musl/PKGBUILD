# Maintainer: Sam <dev at samarthj dot com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

# shellcheck disable=2034,2148,2154

pkgname="uutils-coreutils-musl"
pkgver=r6379.2e251f91f
pkgrel=1
pkgdesc="GNU Coreutils rewritten in Rust - Built with musl libc"
arch=('x86_64')
url='https://github.com/uutils/coreutils'
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'rust' 'rust-musl' 'cargo' 'cmake' 'python-sphinx')
conflicts=('uutils-coreutils-git' 'uutils-coreutils' 'coreutils')
provides=('coreutils' 'uutils-coreutils')
source=("uutils::git+https://github.com/uutils/coreutils.git#commit=2e251f91f1f5c65518f84c4d2219bf3bca50faaf")
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/uutils || exit 1
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/uutils || exit 1
  make PROFILE=release CARGOFLAGS=" --target x86_64-unknown-linux-musl"
}

check() {
  cd ${srcdir}/uutils || exit 1
  # TODO: Fix the failing tests. These 4 seem to fail for a few benign reasons.
  make CARGOFLAGS=" --target x86_64-unknown-linux-musl" PROFILE=release test \
    TEST_NO_FAIL_FAST="--no-fail-fast -- --skip test_chown::test_chown_only_group --skip test_chown::test_chown_only_group_id --skip test_pinky::test_no_flag --skip test_who::test_lookup"
}

package() {
  cd ${srcdir}/uutils || exit 1
  make CARGOFLAGS=" --target x86_64-unknown-linux-musl" install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    MANDIR=/share/man/man1 \
    PROFILE=release
  # below algos implemented in hashsum, fix to keep compat with GNU Coreutils
  for algo in sha1 sha224 sha256 sha384 sha512 md5 b2 sha3{,-224,-256,-384,-512} shake{128,256}
  do
  ln -s "hashsum" "${pkgdir}/usr/bin/${algo}sum"
  done
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}