# Maintainer: Imperator Storm <ImperatorStorm11@protonmail.com>
# Contributor: Sam <dev at samarthj dot com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

# shellcheck disable=2034,2148,2154

pkgname="uutils-coreutils-musl"
pkgver=r7345.db22b15fa
pkgrel=1
pkgdesc="GNU Coreutils rewritten in Rust - Built with musl libc"
arch=($CARCH)
url='https://github.com/uutils/coreutils'
license=('MIT')
makedepends=('git' 'rust' 'rust-musl' 'cargo' 'cmake' 'python-sphinx' 'musl' 'gcc-libs')
conflicts=('uutils-coreutils-git' 'uutils-coreutils' 'coreutils')
provides=('coreutils' 'uutils-coreutils')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        tests.patch
        0001_fix-install.patch)
sha256sums=('SKIP'
            'ae02faf5ca52e0f3005f968e3399d4cb0f736aa2c64c897f8ec856e02fdf11f0')
options=(!lto)

prepare(){
  cd $_pkgname-$pkgver
  sed 's|"bin"|"builduser"|g' -i tests/by-util/test_{chgrp,chown}.rs
  patch -Np1 < ../tests.patch
  patch -tp1 <../0001_fix-install.patch
}

build() {
  cd $_pkgname-$pkgver || exit 1
  # {pinky uptime users who} break with musl libc. See https://github.com/uutils/coreutils/issues/2029.
  make PROFILE=release SKIP_UTILS="pinky uptime users who" CARGOFLAGS=" --target x86_64-unknown-linux-musl --features feat_os_unix_musl"
}

# Tests fail sporadically on musl, see https://github.com/uutils/coreutils/issues/2024.
check() {
  cd $_pkgname-$pkgver || exit 1
  make test \
      PROFILE=release \
      CARGOFLAGS="--release \
      --target x86_64-unknown-linux-musl \
      --features feat_os_unix_musl" \
      SKIP_UTILS="pinky uptime users who" \
      TEST_NO_FAIL_FAST="--no-fail-fast -- \
      --skip test_chown::test_big_p \
      --skip test_chgrp::test_big_p \
      --skip test_chgrp::test_big_h"
}

package() {
  cd $_pkgname-$pkgver || exit 1
  make CARGOFLAGS=" --target x86_64-unknown-linux-musl --features feat_os_unix_musl" install \
    SKIP_UTILS="pinky uptime users who" \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    MANDIR=/share/man/man1 \
    PROFILE=release
  # below algos implemented in hashsum, symlink to keep compat with GNU Coreutils
  for algo in sha1 sha224 sha256 sha384 sha512 md5 b2 sha3{,-224,-256,-384,-512} shake{128,256}
  do
  ln -s "hashsum" "${pkgdir}/usr/bin/${algo}sum"
  done
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}