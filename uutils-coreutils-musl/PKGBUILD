# Maintainer: Sam <dev at samarthj dot com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

# shellcheck disable=2034,2148,2154

pkgname="uutils-coreutils-musl"
pkgver=r6379.2e251f91f
pkgrel=1
pkgdesc="GNU Coreutils rewritten in Rust - Built with musl libc"
arch=($CARCH)
url='https://github.com/uutils/coreutils'
license=('MIT')
makedepends=('git' 'rust' 'rust-musl' 'cargo' 'cmake' 'python-sphinx' 'musl' 'gcc-libs')
conflicts=('uutils-coreutils-git' 'uutils-coreutils' 'coreutils')
provides=('coreutils' 'uutils-coreutils')
source=("uutils::git+https://github.com/uutils/coreutils.git#commit=2e251f91f1f5c65518f84c4d2219bf3bca50faaf"
        0001_fix-install.patch)
sha256sums=('SKIP'
            'ae02faf5ca52e0f3005f968e3399d4cb0f736aa2c64c897f8ec856e02fdf11f0')
prepare(){
  cd ${srcdir}/uutils || exit 1
  patch -tp1 <../0001_fix-install.patch
}
pkgver() {
  cd ${srcdir}/uutils || exit 1
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/uutils || exit 1
  make PROFILE=release SKIP_UTILS="pinky uptime users who" CARGOFLAGS=" --target x86_64-unknown-linux-musl --features feat_os_unix_musl"
  # {pinky uptime users who} break with musl libc. See https://github.com/uutils/coreutils/issues/2029.
}

# Tests fail sporadically on musl, see https://github.com/uutils/coreutils/issues/2024.
#check() {
#  cd ${srcdir}/uutils || exit 1
#  # TODO: Fix the failing tests. These 2 seem to fail for a few benign reasons.
#  make test \
#      PROFILE=release \
#      CARGOFLAGS=--release \
#      TEST_NO_FAIL_FAST="--no-fail-fast -- \
#      --skip test_chown::test_big_p \
#      --skip test_chgrp::test_big_p \
#      --skip test_chgrp::test_big_h"
#}

package() {
  cd ${srcdir}/uutils || exit 1
  make CARGOFLAGS=" --target x86_64-unknown-linux-musl --features feat_os_unix_musl" install \
    SKIP_UTILS="pinky uptime users who" \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    MANDIR=/share/man/man1 \
    PROFILE=release
  # below algos implemented in hashsum, symlink to keep compat with GNU Coreutils
  for algo in sha1 sha224 sha256 sha384 sha512 md5 b2 sha3{,-224,-256,-384,-512} shake{128,256}
  do
  ln -s "hashsum" "${pkgdir}/usr/bin/${algo}sum"
  done
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
