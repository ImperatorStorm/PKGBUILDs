# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=noto-color-emoji-fox
pkgver=2.048.r0.gb3e3051
pkgrel=1
pkgdesc='Google Noto Color Emoji font'
arch=(any)
url='https://www.google.com/get/noto/'
license=(OFL-1.1)
provides=(emoji-font)
makedepends=(git harfbuzz-utils nanoemoji python-nototools python-fonttools pngquant zopfli python-zopfli python-cairo imagemagick)
source=(git+https://github.com/googlefonts/noto-emoji.git#commit=b3e3051a088047d19fd4d49b1c3ac42fb8c3aaf8
        0001-foxxo.patch)
b2sums=('762b230b2a84228bf1adc6225fb501a2d9eee3349bf4fdbb316ad73763cda659e9dc5951e10e812c986c42e5da1f92c34a1ca16fe7d9178ccf46cdc87934b2a4'
        '7486a043d46a30f4fe903dc4aae78184e0f4a34003421e9af5b1eb590748d433e2f7ee4b76f59724075065da83c4310fb0b6e9900fa3557fe3512c4c8b492967')
pkgver() {
  cd noto-emoji
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd noto-emoji
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    src="${src%.zst}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  cp $srcdir/SY.png third_party/region-flags/png/SY.png
  rm -rf third_party/pngquant fonts/*.ttf
}

build() {
  cd noto-emoji
  make BYPASS_SEQUENCE_CHECK='True' VIRTUAL_ENV=1
  mv *.ttf fonts/
  python drop_flags.py fonts/NotoColorEmoji.ttf
  python colrv1_generate_configs.py
  cd colrv1
  rm -rf build/
  nanoemoji *.toml
  cd ..
  cp colrv1/build/NotoColorEmoji.ttf fonts/Noto-COLRv1.ttf
  cp colrv1/build/NotoColorEmoji-noflags.ttf fonts/Noto-COLRv1-noflags.ttf
  python colrv1_postproc.py
  hb-subset --unicodes-file=flags-only-unicodes.txt \
   --output-file=fonts/NotoColorEmoji-flagsonly.ttf \
   fonts/NotoColorEmoji.ttf
  python update_flag_name.py
}

package() {
  cd noto-emoji

  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 fonts/Noto-COLRv1.ttf "$pkgdir"/usr/share/fonts/noto
#   install -m644 fonts/Noto-COLRv1-noflags.ttf "$pkgdir"/usr/share/fonts/noto
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
