# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=noto-color-emoji-fox
pkgver=2.048.r2.g8998f5d
pkgrel=1
pkgdesc='Google Noto Color Emoji font'
arch=(any)
url='https://www.google.com/get/noto/'
license=(OFL-1.1)
provides=(emoji-font)
makedepends=(git harfbuzz-utils nanoemoji python-nototools python-fonttools pngquant zopfli python-zopfli python-cairo imagemagick)
source=(git+https://github.com/googlefonts/noto-emoji.git#commit=8998f5dd683424a73e2314a8c1f1e359c19e8742
        0001-foxxo.patch)
b2sums=('d1a664ae0278965f100c5ee10b2a6dff5b9dd6903f8b1db4943c61647ed1bdbec435829cab585ed3936920f745514d48d1485f1c97266db2d6088d6e8ba3711b'
        '7486a043d46a30f4fe903dc4aae78184e0f4a34003421e9af5b1eb590748d433e2f7ee4b76f59724075065da83c4310fb0b6e9900fa3557fe3512c4c8b492967')
pkgver() {
  cd noto-emoji
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd noto-emoji
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    src="${src%.zst}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  rm -rf third_party/pngquant fonts/*.ttf
}

build() {
  cd noto-emoji
  make BYPASS_SEQUENCE_CHECK='True' VIRTUAL_ENV=1
  mv *.ttf fonts/
  python drop_flags.py fonts/NotoColorEmoji.ttf
  python colrv1_generate_configs.py
  cd colrv1
  rm -rf build/
  nanoemoji *.toml
  cd ..
  cp colrv1/build/NotoColorEmoji.ttf fonts/Noto-COLRv1.ttf
  cp colrv1/build/NotoColorEmoji-noflags.ttf fonts/Noto-COLRv1-noflags.ttf
  python colrv1_postproc.py
  hb-subset --unicodes-file=flags-only-unicodes.txt \
   --output-file=fonts/NotoColorEmoji-flagsonly.ttf \
   fonts/NotoColorEmoji.ttf
  python update_flag_name.py
}

package() {
  cd noto-emoji

  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 fonts/Noto-COLRv1.ttf "$pkgdir"/usr/share/fonts/noto
#   install -m644 fonts/Noto-COLRv1-noflags.ttf "$pkgdir"/usr/share/fonts/noto
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
